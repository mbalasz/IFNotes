apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply from: "$rootDir/ktlint.gradle"

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.example.mateusz.ifnotes"
        minSdkVersion 26
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            testCoverageEnabled true
        }
    }

    sourceSets {
        test {
            java.srcDirs += "$projectDir/src/testShared/java"
        }

        androidTest {
            java.srcDirs += "$projectDir/src/testShared/java"
        }
    }
    testOptions.unitTests.includeAndroidResources = true

    // Gradle automatically adds 'android.test.runner' as a dependency.
    useLibrary 'android.test.runner'

    useLibrary 'android.test.base'
    useLibrary 'android.test.mock'
}

apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

configurations { all*.exclude group: 'com.android.support' }

dependencies {
    def lifecycle_version = "2.0.0"
    def room_version = "2.1.0-beta01"
    def anko_version = '0.10.7'

    implementation "org.jetbrains.anko:anko:$anko_version"
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.android.material:material:1.1.0-alpha06'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
    implementation "androidx.room:room-runtime:$room_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.0-RC2'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.0-RC2'
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-rxjava2:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.0'
    api 'com.google.guava:guava:27.0.1-android'
    implementation 'com.github.PhilJay:MPAndroidChart:v3.1.0'
    implementation 'com.google.dagger:dagger:2.22'
    kapt 'com.google.dagger:dagger-compiler:2.22'
    kapt "com.google.dagger:dagger-android-processor:2.22"
    implementation 'com.google.dagger:dagger-android-support:2.22'
    kaptTest 'com.google.dagger:dagger-compiler:2.22'
    kaptAndroidTest 'com.google.dagger:dagger-compiler:2.22'
//    Temporarily keep mockito downgraded until https://github.com/mockito/mockito/issues/1716 is
//    fixed.
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.1.0"
    testImplementation 'junit:junit:4.12'
    testImplementation "org.robolectric:robolectric:4.0.2"
    testImplementation "org.jetbrains.kotlin:kotlin-test:1.1.51"
    testImplementation "net.bytebuddy:byte-buddy-android:1.9.14"
    testImplementation 'androidx.test:core:1.2.0'
    testImplementation "androidx.arch.core:core-testing:2.0.1"
    testImplementation 'androidx.test.ext:junit:1.1.1'
    testImplementation 'androidx.test.espresso:espresso-core:3.3.0-alpha02'
    testImplementation 'androidx.test.espresso:espresso-contrib:3.3.0-alpha02'
    testImplementation 'androidx.test:rules:1.2.0'
    testImplementation 'androidx.test:runner:1.2.0'
    testImplementation 'org.hamcrest:hamcrest:2.1'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.3.0-RC2'

    androidTestImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.1.0"
    androidTestImplementation "org.mockito:mockito-android:3.0.0"
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation "androidx.arch.core:core-testing:2.0.1"
    androidTestImplementation 'org.hamcrest:hamcrest-library:1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.3.0-alpha02'
    androidTestImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.3.0-RC2'
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

// #### Jacoco ####
apply plugin: 'com.android.application'
apply plugin: 'jacoco'

jacoco {
    toolVersion = "$jacoco_version"
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
}

task jacocoTestReport(type: JacocoReport, dependsOn: ['testDebugUnitTest', 'createDebugCoverageReport']) {

    reports {
        xml.enabled = true
        html.enabled = true
    }

    def fileFilter = [
        '**/R.class',
        '**/R$*.class',
        '**/BuildConfig.*',
        '**/Manifest*.*',
        '**/*Test*.*',
        'android/**/*.*',
        '**/*$[0-9].*',
        '**/*Module.*', // Modules for Dagger.
        '**/*Dagger*.*', // Dagger auto-generated code.
        '**/*MembersInjector*.*', // Dagger auto-generated code.
        '**/*_Provide*Factory*.*',
        '**/*_Factory.*', //Dagger auto-generated code
        '**/*$*$*.*', // Anonymous classes generated by kotlin

    ]
    def debugTree = fileTree(dir: "$project.buildDir/tmp/kotlin-classes/debug", excludes: fileFilter)
    def mainSrc = "$project.projectDir/src/main/java"

    sourceDirectories = files([mainSrc])
    classDirectories = files([debugTree])
    executionData = fileTree(dir: "$buildDir", includes: [
        'jacoco/testDebugUnitTest.exec', 'outputs/code_coverage/debugAndroidTest/connected/**/*.ec'
    ])
}

// ########